/**
 * Technicolor version 1.1
 * Original by DKT70
 * Optimized by CeeJay.dk
 */

cbuffer cbuf : register(b0)
{
    float Power <float uimin = 0.0; float uimax = 8.0;> = 4.0;
    float4 RGBNegativeAmount <bool color = true;> = float4(0.88, 0.88, 0.88, 1);
    float Strength <float uimin = 0.0; float uimax = 1.0;> = 0.4;
}
#include "ReShade.fxh"

float4 TechnicolorPass(float4 vpos : SV_Position, float2 texcoord : TexCoord) : SV_Target
{
	const float3 cyanfilter = float3(0.0, 1.30, 1.0);
	const float3 magentafilter = float3(1.0, 0.0, 1.05);
	const float3 yellowfilter = float3(1.6, 1.6, 0.05);
	const float2 redorangefilter = float2(1.05, 0.620); // RG_
	const float2 greenfilter = float2(0.30, 1.0);       // RG_
	const float2 magentafilter2 = magentafilter.rb;     // R_B

    float4 tcol = ReShade_BackBuffer.SampleLevel(ReShade_sL, texcoord, 0);
	
	float2 negative_mul_r = tcol.rg * (1.0 / (RGBNegativeAmount.r * Power));
	float2 negative_mul_g = tcol.rg * (1.0 / (RGBNegativeAmount.g * Power));
	float2 negative_mul_b = tcol.rb * (1.0 / (RGBNegativeAmount.b * Power));
	float3 output_r = dot(redorangefilter, negative_mul_r).xxx + cyanfilter;
	float3 output_g = dot(greenfilter, negative_mul_g).xxx + magentafilter;
	float3 output_b = dot(magentafilter2, negative_mul_b).xxx + yellowfilter;

	return float4(lerp(tcol.rgb, output_r * output_g * output_b, Strength), tcol.a);
}

technique Technicolor
{
	pass
	{
		VertexShader = PostProcessVS;
		PixelShader = TechnicolorPass;
	}
}
