//@author:
//@help:
//@tags: filter
//@credits:

#if !defined(PASSFORMAT0)
#define PASSFORMAT0 "R16G16B16A16_Float"
#endif

#define DISCSAMPLES 8
#define DIFFENVSRC Environment

#include <packs/mp.fxh/depthreconstruct.fxh>
#include <packs/mp.fxh/DiscSample.fxh>
#include <packs/mp.fxh/quaternion.fxh>

Texture2D Albedo : PREVIOUS;
//rgb: normal, a: matid
Texture2D Normal : NORMAL;
Texture2D VelUV : VELUV;
Texture2D<float2> RoughMetal : ROUGHMETAL;
Texture2D Bckg;
Texture2D<float> Depth : UDEPTH;
Texture2D<uint4> stencil : USTENCIL;
Texture2D Environment;

float2 R:TARGETSIZE;

cbuffer controls:register(b0)
{
	float4x4 EnvTr;
	float BckgEnv = 0;
	float EnvBright = 1;
    float diffbluradd <string uiname="Non-metal Diffuse Blur Offset";> = 0.5;
	
};
cbuffer semantics:register(b1)
{
    float4x4 tV : CVIEW;
    float4x4 tVI : CVIEWINV;
    float4x4 tP : CPROJ;
    float4x4 tPI : CPROJINV;
};

SamplerState sT <bool visible=true;string uiname="Texture Sampler";>
{
    Filter=MIN_MAG_MIP_LINEAR;
    AddressU=WRAP;
    AddressV=WRAP;
};
SamplerState sE <bool visible=true;string uiname="Environment Sampler";>
{
    Filter=MIN_MAG_MIP_LINEAR;
    AddressU=WRAP;
    AddressV=WRAP;
};

float4 pIBL(float4 PosWVP:SV_POSITION,float2 x:TEXCOORD0):SV_Target{
    int3 puv = int3((int2)(x*R), 0);
    uint4 stcl = stencil.Load(puv);
	float4 bckg = Bckg.SampleLevel(sE, x, 0);
    if(any(stcl > 0))
    {
    	
        float4 albedoin = Albedo.Load(puv);
    	if(any(stcl>127)) return albedoin;
    	
        float4 normid = Normal.Load(puv);
        float2 uvin = VelUV.Load(puv).zw;
    	
	    float3 posV = UVZtoVIEW(x, Depth.Load(puv), tP, tPI);
		float4x4 dirm = mul(tVI, EnvTr);
		float3 dir = mul(float4(normalize(posV), 0), dirm).xyz;

        float2 roughmetal = RoughMetal.Load(puv);
        float rough = roughmetal.r;
        float metal = lerp(0, 1, roughmetal.g);
        float4 col = albedoin;
    	float3 norm = normid.xyz*2-1;
        float3 eye = normalize(posV);
        float3 rdir = mul(float4(reflect(eye, norm), 0), dirm).xyz;

    	float mipl;
    	uint nope;
    	Environment.GetDimensions(0, nope, nope, mipl);
        mipl = max(0, mipl-1);

    	float3 EnvSharp = DiscSample(Environment, sE, rdir, rough, mipl).rgb * EnvBright;
    	float3 EnvNm = 0;
        if(metal < 1)
    	{
    		EnvNm = DiscSample(DIFFENVSRC, sE, rdir, (rough * 0.5) + diffbluradd, mipl).rgb * EnvBright;
    		//EnvNm = AnisotropicSample(DIFFENVSRC, sE, rdir, rtan, rbin, (rough * 0.5) + diffbluradd, 0.25, mipl).rgb;
    	}

        col.rgb = lerp(col.rgb * EnvNm + 0.03*EnvSharp * lerp(1, albedoin.rgb, rough), col.rgb * EnvSharp, metal);
        float fresnel = pow(abs(dot(norm, eye)), 0.21 );
        //col.rgb = lerp(EnvSharp, col.rgb, fresnel);
    	col.rgb = lerp(bckg.rgb, col.rgb, col.a);
        return col;
    }
    else
	{
		return bckg;
	}
}
technique10 IBL{
	pass P0{SetPixelShader(CompileShader(ps_5_0,pIBL()));}
}
